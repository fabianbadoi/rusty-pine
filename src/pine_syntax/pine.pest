pine = { SOI ~ operation ~ ("|" ~ operation)* ~ EOI }
operation = _{ (compound_expression | from | select | filters | join) }

compound_expression = { table_name ~ (value | filter) }

from = { ("from:" | "f:") ~ table_name }
join = { ("join:" | "j:") ~ table_name }
table_name = { sql_name }

select = { ("select:" | "s:") ~ column_name ~ ("," ~ column_name)* }

filters = { ("where:" | "w:") ~ filter+ }
filter = { column_name ~ condition }
condition = _{ equals }

equals = { "=" ~ value }
column_name = { sql_name }
value = _{ numeric_value }
numeric_value = { (ASCII_DIGIT | "_")+ }

sql_name = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }

// special rule, makes all ~ accept whitespace
WHITESPACE = _{ " " }
