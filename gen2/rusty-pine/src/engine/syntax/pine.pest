// pine = { SOI ~ simple_compound_expression ~ ("|" ~ operation)* ~ terminal_meta_operation? ~ EOI }
root = { SOI ~ base ~ ( "|" ~ pine)* ~ EOI }
base = { ("f:" | "from:")? ~ table }

pine = _{ select_pine | join_pine  }
select_pine = { ("s:" | "select:") ~ computation+ }
join_pine = _{ explicit_join_pine }
explicit_join_pine = { ("j:" | "join:") ~ table ~ computation ~ "=" ~ computation }
// todo multiple columns

// Computations are either columns or function calls. They're the things you can put in SELECT or WHERE clauses
// (and more).
computation = { function_call | column }
function_call = { sql_name ~ "(" ~ computation* ~ ")"}

column = { db_table_column_name | table_column_name | column_name }
column_name = { sql_name }
table_column_name = { sql_name ~ "." ~ sql_name }
db_table_column_name = { table ~ "." ~ sql_name }

table = { db_table_name | sql_name }
db_table_name = { sql_name ~ "." ~ sql_name }
sql_name = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }

WHITESPACE = _{ " " | "\t" }
