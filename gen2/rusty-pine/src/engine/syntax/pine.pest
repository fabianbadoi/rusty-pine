// pine = { SOI ~ simple_compound_expression ~ ("|" ~ operation)* ~ terminal_meta_operation? ~ EOI }
root = { SOI ~ base ~ ( "|" ~ pine)* ~ EOI }
base = { ("f:" | "from:")? ~ table }

pine               = _{ select_pine | join_pine }
select_pine        = { ("s:" | "select:") ~ selectable+ }
join_pine          = _{ explicit_join_pine }
explicit_join_pine = { ("j:" | "join:") ~ table ~ condition+ }

// Selectables are columns, conditions, values or function calls. They're the things you can put in SELECT or WHERE
// clauses (and more).
selectable    = { condition | computation }
computation   = { function_call | column | literal_value }
function_call = { sql_name ~ "(" ~ computation* ~ ")"}
condition     = { computation ~ comparison_symbol ~ computation }

column = { db_table_column_name | table_column_name | column_name }
column_name = { sql_name }
table_column_name = { sql_name ~ "." ~ sql_name }
db_table_column_name = { table ~ "." ~ sql_name }

table = { db_table_name | sql_name }
db_table_name = { sql_name ~ "." ~ sql_name }
sql_name = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }

// values
literal_value = { numeric_value | string_value }
numeric_value = { (ASCII_DIGIT | "_")+ ~ ("." ~ (ASCII_DIGIT | "_")+)? }
string_value  = { quote_string_value | apostrophe_string_value }

// strings are expressed this way to more easily support escape characters
quote_string_value = ${ "\"" ~ quote_string_inner ~ "\"" }
quote_string_inner = @{ quote_string_char* }
quote_string_char  = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

apostrophe_string_value = ${ "'" ~ apostrophe_string_inner ~ "'" }
apostrophe_string_inner = @{ apostrophe_string_char* }
apostrophe_string_char  = {
    !("'" | "\\") ~ ANY
    | "\\" ~ ("'" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

comparison_symbol = { "=" | "!="  | ">" | ">=" | "<" | "<=" }

WHITESPACE = _{ " " | "\t" }
