pine = { SOI ~ simple_compound_expression ~ ("|" ~ operation)* ~ meta_operation? ~ EOI }

operation = _{ simple_operation | compound_operation }
simple_operation = _{ from | select | unselect | filters | join | group_by | order | limit }
compound_operation = _{ explicit_join_compound_expression | simple_compound_expression }
meta_operation = _{ show_neighbours }

simple_compound_expression = { table_name ~ (value | filter)* }
explicit_join_compound_expression = { explicit_join_reference  ~ (value | filter)* }
explicit_join_reference = { table_name ~ "." ~ column_name }

// actual pines
from = { ("from:" | "f:") ~ table_name }
join = { ("join:" | "j:") ~ table_name }
select = { ("select:" | "s:") ~ operand+ }
unselect = { ("unselect:" | "u:") ~ operand+ }
filters = { ("where:" | "w:") ~ filter+ }
group_by = { ("group:" | "g:") ~ operand+ }
order = { ("order:" | "o:") ~ ordering ~ ("," ~ ordering)* }
limit = { ("limit:" | "l:") ~ numeric_value }
show_neighbours = { "|" } // ending a query in a pipe shows connected tables

operand = { function_call | identified_column | value }

function_call = { function_name ~ "(" ~ function_operand ~ ")" }
function_name = @{ sql_name }
function_operand = { identified_column | constant }

constant = { number }
number = { ("+"|"-")? ~ ASCII_DIGIT+ }

// identifiers
table_name = { sql_name }
column_name = { sql_name }
identified_column = { fully_qualified_column | column_name }
fully_qualified_column = { table_name ~ "." ~ column_name }
sql_name = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }


// conditions
filter = { binary_filter | unary_filter }
unary_filter = { filter_is_null | filter_is_not_null }
binary_filter = { operand ~ operator ~ operand }

filter_is_null = { operand ~ "?" }
filter_is_not_null = { operand ~ "!?" }

operator = _{ optr_eq | optr_ne | optr_gte | optr_gt | optr_lte | optr_lt }
optr_eq  = { "="  }
optr_ne  = { "!=" }
optr_gt  = { ">"  }
optr_gte = { ">=" }
optr_lt  = { "<"  }
optr_lte = { "<=" }


// orders
ordering = { ordering_asc | ordering_desc }
ordering_desc = { operand ~ direction_desc? }
ordering_asc  = { operand ~ direction_asc }
direction_asc = _{ "asc" | "ASC" | "+" }
direction_desc = _ { "desc" | "DESC" | "-" }


// values
value = { numeric_value | string_value }

numeric_value = { (ASCII_DIGIT | "_")+ }

string_value = { quote_string_value | apostrophe_string_value }

quote_string_value = ${ "\"" ~ quote_string_inner ~ "\"" }
quote_string_inner = @{ quote_string_char* }
quote_string_char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

apostrophe_string_value = ${ "'" ~ apostrophe_string_inner ~ "'" }
apostrophe_string_inner = @{ apostrophe_string_char* }
apostrophe_string_char = {
    !("'" | "\\") ~ ANY
    | "\\" ~ ("'" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}


// special rule, makes all ~ accept whitespace
WHITESPACE = _{ " " }
